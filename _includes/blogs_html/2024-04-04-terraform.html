<h2 id="overview">Overview</h2>
<p><strong>Terraform</strong> is an open-source infrastructure as code (IaC) tool that allows you to define and provision infrastructure
using declarative scripts. The core lifecycle commands of Terraform are:</p>
<ul>
<li><code>init</code>: Initialize a new or existing Terraform configuration.</li>
<li><code>plan</code>: Create an execution plan.</li>
<li><code>apply</code>: Apply the changes required to reach the desired state of the configuration.</li>
<li><code>destroy</code>: Destroys everything you provisioned.</li>
</ul>
<p>In this guide, we will use Terraform to create three KVM virtual machines (VMs): <code>base</code>, <code>client</code>, and <code>xdp</code>.</p>
<ul>
<li><strong>Base</strong>: This is the base QEWU server VM.</li>
<li><strong>XDP</strong>: This is the base VM with XDP (eBPF program) loaded.</li>
<li><strong>Client</strong>: This VM will be used to measure the throughput of the <code>base</code> and <code>xdp</code> VMs.</li>
</ul>
<p>For simplicity, the <code>.img</code> files for the VMs will initially be the same as the Ubuntu 24.04 Server Cloud Image.</p>
<p>Here’s a brief overview of what each VM will represent:</p>
<ul>
<li><strong><code>base_root.img</code></strong>: Image for the <code>base</code> VM.</li>
<li><strong><code>xdp_root.img</code></strong>: Image for the <code>xdp</code> VM.</li>
<li><strong><code>client_root.img</code></strong>: Image for the <code>client</code> VM.</li>
</ul>
<h2 id="downloading-the-base-cloud-image">Downloading the Base Cloud Image</h2>
<p>First, download the base cloud image <code>.img</code> file from the official Ubuntu cloud image repository:</p>
<p><a href="https://cloud-images.ubuntu.com/releases/24.04/release/ubuntu-24.04-server-cloudimg-amd64.img">Download Ubuntu 24.04 Server Cloud Image</a></p>
<p>Store the downloaded image in the <code>images/</code> directory.</p>
<h2 id="directory-structure">Directory Structure</h2>
<p>Here is the directory structure for your Terraform configuration:</p>
<pre><code class="language-plaintext">.
├── cloudinit_base_template.tf
├── cloudinit_base.tf
├── cloudinit_client.tf
├── cloudinit_xdp.tf
├── common_domain.tf
├── images
│   ├── base_root.img
│   ├── client_root.img
│   ├── ubuntu-24.04-server-cloudimg-amd64.img
│   └── xdp_root.img
├── main.tf
├── providers.tf
├── terraform.tfvars
└── variables.tf
</code></pre>
<h1 id="cloudinit_base_templatetf"><code>cloudinit_base_template.tf</code></h1>
<pre><code class="language-hcl">data &quot;template_file&quot; &quot;cloudinit_template&quot; {
  template = &lt;&lt;EOF
#cloud-config
fqdn: ${var.vm_domain}
manage_etc_hosts: true
package_update: true
package_upgrade: true
ssh_pwauth: 1
timezone: &quot;America/New_York&quot;
users:
 - name: user
   passwd: $y$j9T$90KTrNAm1VThpSJR99lD/1$V28A3c/tiTBCC1drC6jnu4LVChD1XpcLA2uQ.RD6uXB
   home: /home/user
   shell: /bin/bash
   sudo: ALL=(ALL) NOPASSWD:ALL
   lock_passwd: false
   chpasswd: { expire: False }
   groups: sudo, users, admin
EOF

  vars = {
    fqdn = var.vm_domain
  }
}
</code></pre>
<h1 id="cloudinit_basetf"><code>cloudinit_base.tf</code></h1>
<pre><code class="language-hcl">data &quot;template_file&quot; &quot;cloudinit_base&quot; {
  template = data.template_file.cloudinit_template.rendered
  vars = {
    fqdn = &quot;base.${var.vm_domain}&quot;
  }
</code></pre>
<h1 id="cloudinit_xdptf"><code>cloudinit_xdp.tf</code></h1>
<pre><code class="language-hcl">data &quot;template_file&quot; &quot;cloudinit_xdp&quot; {
  template = data.template_file.cloudinit_template.rendered
  vars = {
    fqdn = &quot;xdp.${var.vm_domain}&quot;
  }
}
</code></pre>
<h1 id="cloudinit_clienttf"><code>cloudinit_client.tf</code></h1>
<pre><code class="language-hcl">data &quot;template_file&quot; &quot;cloudinit_client&quot; {
  template = data.template_file.cloudinit_template.rendered
  vars = {
    fqdn = &quot;client.${var.vm_domain}&quot;
  }
}
</code></pre>
<h1 id="common_domaintf"><code>common_domain.tf</code></h1>
<pre><code class="language-hcl"># common_domain.tf


locals {
  common_memory_vcpu = {
    memory = var.vm_memory
    vcpu   = var.vm_vcpu
  }


  common_console = [
    {
      type        = &quot;pty&quot;
      target_port = &quot;0&quot;
      target_type = &quot;serial&quot;
    },
    {
      type        = &quot;pty&quot;
      target_type = &quot;virtio&quot;
      target_port = &quot;1&quot;
    }
  ]


  common_graphics = {
    type        = &quot;spice&quot;
    listen_type = &quot;address&quot;
    autoport    = true
  }
}
</code></pre>
<h1 id="providerstf"><code>providers.tf</code></h1>
<pre><code class="language-hcl"># Define Terraform configuration requirements and providers
terraform {
  required_version = &quot;&gt;= 1.9.2&quot;


  required_providers {
    random = {
      source  = &quot;hashicorp/random&quot;
      # version = &quot;~&gt; 3.6.1&quot; # Allows any version &gt;= 3.6.1 and &lt; 3.7.0
      version = &quot;~&gt; 3.6.1&quot; # Use the latest patch version of 3.6.x
    }
    libvirt = {
      source  = &quot;dmacvicar/libvirt&quot;
      version = &quot;~&gt; 0.7.6&quot; # Use the latest patch version of 0.7.x
    }
  }


  # Optional: Configure backend for state storage
  # backend &quot;s3&quot; {
  #   bucket         = &quot;my-tf-state&quot;
  #   key            = &quot;terraform/state&quot;
  #   region         = &quot;us-east-1&quot;
  # }
}
</code></pre>
<h1 id="terraformtfvars"><code>terraform.tfvars</code></h1>
<pre><code class="language-hcl"># terraform.tfvars is a file where you provide values for the variables defined in variables.tf.
# This file is used to set or override the default values of variables.


# Purpose
# Assign Values: You use terraform.tfvars to assign actual values to the variables that were declared in variables.tf.
# Override Defaults: If a variable has a default value in variables.tf, the value in terraform.tfvars will override it.
# Provide Environment-Specific Configurations: You can use this file to specify different values
# for different environments or scenarios.


# VM Configurations
vms = {
  client = {
    hostname = &quot;client&quot;
    vm_ip     = [&quot;192.168.100.2&quot;]
  },
  base = {
    hostname = &quot;base&quot;
    vm_ip     = [&quot;192.168.100.3&quot;]
  },
  xdp = {
    hostname = &quot;xdp&quot;
    vm_ip     = [&quot;192.168.100.4&quot;]
  },
}


# Node Configuration Parameters
vm_vcpu      = 2
vm_memory    = 4096  # Memory in MiB
vm_disksize  = 40    # Disk size in GiB
vm_domain    = &quot;local&quot;


# VM Image Paths
vm_baseimage = &quot;images/ubuntu-24.04-server-cloudimg-amd64.img&quot;
client_image = &quot;images/client_root.img&quot;
base_image   = &quot;images/base_root.img&quot;
xdp_image     = &quot;images/xdp_root.img&quot;


# Host Parameters
libvirt_disk_path = &quot;/var/lib/libvirt/images/kernel_absorb&quot;
</code></pre>
<h1 id="variablestf"><code>variables.tf</code></h1>
<pre><code class="language-hcl"># variables.tf is a Terraform configuration file where you define variables that will be used throughout
# your Terraform configuration. This file contains variable declarations, including descriptions,
# types, and default values.




# Purpose
# Declare Variables: You use variables.tf to declare what variables your Terraform configuration requires.
# Provide Defaults: You can specify default values for variables, which will be used if no other value is provided.
# Describe Variables: You can include descriptions to explain what each variable is for, which helps with
# documentation and clarity.


# Host Parameters
variable &quot;libvirt_disk_path&quot; {
  description = &quot;Path for the libvirt pool&quot;
  type        = string
  default     = &quot;/var/lib/libvirt/images/kernel_absorb&quot;
}


# Node Config
variable &quot;vm_baseimage&quot; {
  description = &quot;Path to the Ubuntu 24.04 base image&quot;
  type        = string
  default     = &quot;images/ubuntu-24.04-server-cloudimg-amd64.img&quot;
}


variable &quot;client_image&quot; {
  description = &quot;Path to the client VM image&quot;
  type        = string
  default     = &quot;images/client_root.img&quot;
}


variable &quot;base_image&quot; {
  description = &quot;Path to the base VM image&quot;
  type        = string
  default     = &quot;images/base_root.img&quot;
}


variable &quot;xdp_image&quot; {
  description = &quot;Path to the XDP VM image&quot;
  type        = string
  default     = &quot;images/xdp_root.img&quot;
}


variable &quot;vm_vcpu&quot; {
  description = &quot;Number of vCPUs for each VM&quot;
  type        = number
  default     = 2
}


variable &quot;vm_memory&quot; {
  description = &quot;Memory in MiB for each VM&quot;
  type        = number
  default     = 4096
}


variable &quot;vm_disksize&quot; {
  description = &quot;Disk size in GiB for each VM&quot;
  type        = number
  default     = 40
}


variable &quot;vm_domain&quot; {
  description = &quot;FQDN for the network&quot;
  type        = string
  default     = &quot;local&quot;
}


# VMs
variable &quot;vms&quot; {
  description = &quot;Map of VM configurations&quot;
  type = map(object({
    hostname = string
    vm_ip    = list(string)
  }))
}
</code></pre>
<h1 id="maintf"><code>main.tf</code></h1>
<pre><code class="language-hcl">provider &quot;libvirt&quot; {
  uri = &quot;qemu:///system&quot;
}


resource &quot;libvirt_pool&quot; &quot;kernel_absorb_pool&quot; {
  name = &quot;kernel_absorb_pool&quot;
  type = &quot;dir&quot;
  path = var.libvirt_disk_path
}


# this denotes official cloud .img file
resource &quot;libvirt_volume&quot; &quot;ubuntu_24_base&quot; {
  name   = &quot;ubuntu_24.04&quot;
  pool   = libvirt_pool.kernel_absorb_pool.name
  format = &quot;qcow2&quot;
  source = var.vm_baseimage
}


# client VM storage volume
resource &quot;libvirt_volume&quot; &quot;qcow2_client&quot; {
  name   = &quot;client_root.img&quot;
  pool   = libvirt_pool.kernel_absorb_pool.name
  format = &quot;qcow2&quot;
  source = var.client_image
}


# base server VM storage volume
resource &quot;libvirt_volume&quot; &quot;qcow2_base&quot; {
  name   = &quot;base_root.img&quot;
  pool   = libvirt_pool.kernel_absorb_pool.name
  format = &quot;qcow2&quot;
  source = var.base_image
}


# xdp server VM storage volume
resource &quot;libvirt_volume&quot; &quot;qcow2_xdp&quot; {
  name   = &quot;xdp_root.img&quot;
  pool   = libvirt_pool.kernel_absorb_pool.name
  format = &quot;qcow2&quot;
  source = var.xdp_image
}


resource &quot;libvirt_cloudinit_disk&quot; &quot;cloudinit_client&quot; {
  name      = &quot;cloudinit_client.iso&quot;
  pool      = libvirt_pool.kernel_absorb_pool.name
  user_data = data.template_file.cloudinit_client.rendered
}


resource &quot;libvirt_cloudinit_disk&quot; &quot;cloudinit_base&quot; {
  name      = &quot;cloudinit_base.iso&quot;
  pool      = libvirt_pool.kernel_absorb_pool.name
  user_data = data.template_file.cloudinit_base.rendered
}


resource &quot;libvirt_cloudinit_disk&quot; &quot;cloudinit_xdp&quot; {
  name      = &quot;cloudinit_xdp.iso&quot;
  pool      = libvirt_pool.kernel_absorb_pool.name
  user_data = data.template_file.cloudinit_xdp.rendered
}


# see https://github.com/dmacvicar/terraform_provider_libvirt/blob/v0.7.6/website/docs/r/network.markdown
resource &quot;libvirt_network&quot; &quot;net&quot; {
  name   = &quot;net&quot;
  mode   = &quot;nat&quot;
  domain = var.vm_domain
  addresses = [&quot;192.168.100.0/24&quot;]
  dhcp {
    enabled = false
  }
  dns {
    enabled    = true
    local_only = false
  }
}


resource &quot;libvirt_domain&quot; &quot;domain_client&quot; {
  name   = var.vms[&quot;client&quot;].hostname


  cloudinit = libvirt_cloudinit_disk.cloudinit_client.id


  network_interface {
    network_name   = &quot;net&quot;
    wait_for_lease = true
    hostname       = var.vms[&quot;client&quot;].hostname
    addresses = var.vms[&quot;client&quot;].vm_ip
  }


  disk {
    volume_id = libvirt_volume.qcow2_client.id
  }


  # common part
  memory = local.common_memory_vcpu.memory
  vcpu   = local.common_memory_vcpu.vcpu


  dynamic &quot;console&quot; {
    for_each = local.common_console
    content {
      type        = console.value.type
      target_port = console.value.target_port
      target_type = console.value.target_type
    }
  }


  graphics {
    type        = local.common_graphics.type
    listen_type = local.common_graphics.listen_type
    autoport    = local.common_graphics.autoport
  }


}


resource &quot;libvirt_domain&quot; &quot;domain_base&quot; {
  name   = var.vms[&quot;base&quot;].hostname


  cloudinit = libvirt_cloudinit_disk.cloudinit_base.id


  network_interface {
    network_name   = &quot;net&quot;
    wait_for_lease = true
    hostname       = var.vms[&quot;base&quot;].hostname
    addresses = var.vms[&quot;base&quot;].vm_ip
  }


  disk {
    volume_id = libvirt_volume.qcow2_base.id
  }


  # common part
  memory = local.common_memory_vcpu.memory
  vcpu   = local.common_memory_vcpu.vcpu


  dynamic &quot;console&quot; {
    for_each = local.common_console
    content {
      type        = console.value.type
      target_port = console.value.target_port
      target_type = console.value.target_type
    }
  }


  graphics {
    type        = local.common_graphics.type
    listen_type = local.common_graphics.listen_type
    autoport    = local.common_graphics.autoport
  }


}


resource &quot;libvirt_domain&quot; &quot;domain_xdp&quot; {
  name   = var.vms[&quot;xdp&quot;].hostname


  cloudinit = libvirt_cloudinit_disk.cloudinit_xdp.id


  network_interface {
    network_name   = &quot;net&quot;
    wait_for_lease = true
    hostname       = var.vms[&quot;xdp&quot;].hostname
    addresses = var.vms[&quot;xdp&quot;].vm_ip
  }


  disk {
    volume_id = libvirt_volume.qcow2_xdp.id
  }


  # common part
  memory = local.common_memory_vcpu.memory
  vcpu   = local.common_memory_vcpu.vcpu


  dynamic &quot;console&quot; {
    for_each = local.common_console
    content {
      type        = console.value.type
      target_port = console.value.target_port
      target_type = console.value.target_type
    }
  }


  graphics {
    type        = local.common_graphics.type
    listen_type = local.common_graphics.listen_type
    autoport    = local.common_graphics.autoport
  }


}
</code></pre>
<h1 id="images"><code>images/</code></h1>
<pre><code class="language-c">upgautamvt@fedora:~/CLionProjects/KernelWithBpfPrograms/benchmarks/macrobenchmarks/terraform/xdp_terraform$ ls images/
base_root.img  client_root.img  ubuntu-24.04-server-cloudimg-amd64.img  xdp_root.img
</code></pre>
<p>Refer
to: <a href="https://github.com/upgautamvt/KernelWithBpfPrograms/tree/main/benchmarks/macrobenchmarks/terraform/xdp_terraform">https://github.com/upgautamvt/KernelWithBpfPrograms/tree/main/benchmarks/macrobenchmarks/terraform/xdp_terraform</a></p>
